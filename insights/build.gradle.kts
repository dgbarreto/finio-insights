import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import java.util.Properties

val localProperties = Properties().apply {
    val file = rootProject.file("local.properties")
    if (file.exists()) load(file.inputStream())
}

val publishVersion = System.getenv("BITRISE_GIT_TAG")
    ?: localProperties["version"] as String?
    ?: "0.1.0-SNAPSHOT"

plugins {
    alias(libs.plugins.kotlinMultiplatform)
    alias(libs.plugins.androidMultiplatformLibrary)
    alias(libs.plugins.kotlinSerialization)
    id("maven-publish")
}

kotlin {
    listOf(
        iosArm64(),
        iosSimulatorArm64()
    ).forEach { iosTarget ->
        iosTarget.binaries.framework {
            baseName = "Insights"
            isStatic = true
        }
    }

    androidLibrary {
        namespace = "dev.finio.insights"
        compileSdk = libs.versions.android.compileSdk.get().toInt()
        minSdk = libs.versions.android.minSdk.get().toInt()

        compilerOptions {
            jvmTarget = JvmTarget.JVM_11
        }
    }

    sourceSets {
        commonMain.dependencies {
            implementation(libs.ktor.client.core)
            implementation(libs.ktor.client.content.negotiation)
            implementation(libs.ktor.serialization.kotlinx.json)
            implementation(libs.ktor.client.logging)
            implementation(libs.kotlinx.coroutines.core)
            implementation(libs.kotlinx.serialization.json)
            implementation(libs.koin.core)
        }

        androidMain.dependencies {
            implementation(libs.ktor.client.android)
        }

        iosMain.dependencies {
            implementation(libs.ktor.client.darwin)
        }
    }
}

publishing {
    publications {
        withType<MavenPublication> {
            groupId = "dev.finio"
            version = publishVersion
            artifactId = when (name) {
                "android" -> "insights-android"
                "iosArm64" -> "insights-iosarm64"
                "iosSimulatorArm64" -> "insights-iossimulatorarm64"
                "kotlinMultiplatform" -> "insights-kmp"
                else -> "insights-$name"
            }

            pom {
                name.set("Finio Insights")
                description.set("KMP insights module for the Finio platform")
                url.set("https://github.com/dgbarreto/finio-insights")

                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licences/MIT")
                    }
                }

                developers {
                    developer {
                        id.set("dgbarreto")
                        name.set("Danilo Barreto")
                        email.set("dgbarreto@gmail.com")
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/dgbarreto/finio-insights")
            credentials {
                username = localProperties["github.actor"] as String?
                    ?: System.getenv("GITHUB_ACTOR") ?: ""
                password = localProperties["github.token"] as String?
                    ?: System.getenv("GITHUB_TOKEN") ?: ""
            }
        }
    }
}